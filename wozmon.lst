
; ******** Source: wozmon.asm
     1                          ; wozmon.asm
     2                          ;
     3                          ; Originally from Apple-1 Operation Manual, Steve Wozniak, 1976
     4                          ; Revised 2024 May 8 for Commodore 64/VIC/128 by David R. Van Wagner davevw.com
     5                          ; * Using C64 KERNAL (instead of MC6520 and KBD/CRT)
     6                          ; * extra processing for expected mark parity, software caps lock, and revised newline/carriage return processing
     7                          ; * revised to expect terminal line edit mode instead of echo off character processing
     8                          ; * revised to acme syntax
     9                          ; * different zero page usage
    10                          ; * changed l/h to wl/wh because vice didn't like that symbol
    11                          ; * reverse toggle instead of spaces only on vic-20 (like HESMON) because too few columns
    12                          
    13                          ; zero page usage - tape stuff on vic-20, 64, 128.  Needs to change for PET, TED, Plus/4, 16, etc.
    14                          xaml=$a3
    15                          xamh=$a4
    16                          stl=$a5
    17                          sth=$a6
    18                          wl=$a7
    19                          wh=$a8
    20                          ysav=$a9
    21                          mode=$aa
    22                          
    23                          in=$200 ; same as Commodore uses, should be fine to copy from/to this, will probably use slightly less
    24                          
    25                          ;** C64, etc. support added by David R. Van Wagner davevw.com ***************************************
    26                          ; Commodore KENRAL
    27                          CHROUT=$FFD2
    28                          CHRIN=$FFCF
    29                          ;** C64 etc. support added by David R. Van Wagner davevw.com ***************************************
    30                          
    31                          * = $cf00
    32                          START:
    33  cf00 d8                 	cld
    34  cf01 58                 	cli
    35  cf02 4c18cf             	jmp escape
    36                          	
    37                          ;** C64, etc. support added by David R. Van Wagner davevw.com ***************************************
    38                          KBD_IN:
    39  cf05 8422               	sty $22
    40  cf07 20cfff             	jsr CHRIN ; note: full screen editor
    41  cf0a a422               	ldy $22
    42  cf0c 60                 	rts
    43                          ;** C64, etc. support added by David R. Van Wagner davevw.com ***************************************
    44                          
    45                          notcr:
    46  cf0d c9df               	cmp #$DF ; underscore or Commodore back arrow (rub out?)
    47  cf0f f013               	beq backspace
    48  cf11 c983               	cmp #$83
    49  cf13 f003               	beq escape
    50  cf15 c8                 	iny
    51  cf16 100f               	bpl nextchar
    52                          escape:
    53  cf18 a9dc               	lda #$DC ; backslash
    54  cf1a 20e7cf             	jsr echo
    55                          getline:
    56  cf1d a90d               	lda #13
    57  cf1f 20e7cf             	jsr echo
    58  cf22 a001               	ldy #1
    59                          backspace:
    60  cf24 88                 	dey
    61  cf25 30f6               	bmi getline
    62                          nextchar:
    63  cf27 2005cf             	jsr KBD_IN
    64  cf2a 0980               	ora #$80
    65  cf2c 990002             	sta in, y
    66                          	;jsr echo - needed only if terminal echo off, line editing off
    67  cf2f c98d               	cmp #$8D
    68  cf31 d0da               	bne notcr
    69  cf33 a0ff               	ldy #$ff
    70  cf35 a900               	lda #$00
    71  cf37 aa                 	tax
    72                          setstor:
    73  cf38 0a                 	asl
    74                          setmode:
    75  cf39 85aa               	sta mode
    76                          blskip:
    77  cf3b c8                 	iny
    78                          nextitem:
    79  cf3c b90002             	lda in, y
    80  cf3f c98d               	cmp #$8D
    81  cf41 f0da               	beq getline
    82  cf43 c9ae               	cmp #$AE ; period
    83  cf45 90f4               	bcc blskip
    84  cf47 f0f0               	beq setmode
    85  cf49 c9ba               	cmp #$BA ; colon
    86  cf4b f0eb               	beq setstor
    87  cf4d c9d2               	cmp #$D2 ; R
    88  cf4f f03b               	beq run
    89  cf51 86a7               	stx wl
    90  cf53 86a8               	stx wh
    91  cf55 84a9               	sty ysav
    92                          nexthex:
    93  cf57 b90002             	lda in, y
    94  cf5a 49b0               	eor #$B0
    95  cf5c c90a               	cmp #$0A
    96  cf5e 9006               	bcc dig
    97  cf60 6988               	adc #$88
    98  cf62 c9fa               	cmp #$FA
    99  cf64 9011               	bcc nothex
   100                          dig:
   101  cf66 0a                 	asl
   102  cf67 0a                 	asl
   103  cf68 0a                 	asl
   104  cf69 0a                 	asl
   105  cf6a a204               	ldx #4
   106                          hexshift:
   107  cf6c 0a                 	asl
   108  cf6d 26a7               	rol wl
   109  cf6f 26a8               	rol wh
   110  cf71 ca                 	dex
   111  cf72 d0f8               	bne hexshift
   112  cf74 c8                 	iny
   113  cf75 d0e0               	bne nexthex
   114                          nothex:
   115  cf77 c4a9               	cpy ysav
   116  cf79 f09d               	beq escape
   117  cf7b 24aa               	bit mode
   118  cf7d 5010               	bvc notstor
   119  cf7f a5a7               	lda wl
   120  cf81 81a5               	sta (stl, x)
   121  cf83 e6a5               	inc stl
   122  cf85 d0b5               	bne nextitem
   123  cf87 e6a6               	inc sth
   124                          tonextitem:
   125  cf89 4c3ccf             	jmp nextitem
   126                          run:
   127  cf8c 6ca300             	jmp (xaml)
   128                          notstor:
   129  cf8f 302b               	bmi xamnext
   130  cf91 a202               	ldx #2
   131                          setadr:
   132  cf93 b5a6               	lda wl-1,x
   133  cf95 95a4               	sta stl-1,x
   134  cf97 95a2               	sta xaml-1,x
   135  cf99 ca                 	dex
   136  cf9a d0f7               	bne setadr
   137                          nxtprnt:
   138  cf9c d014               	bne prdata
   139  cf9e a90d               	lda #13
   140  cfa0 20e7cf             	jsr echo
   141  cfa3 a5a4               	lda xamh
   142  cfa5 20d4cf             	jsr prbyte
   143  cfa8 a5a3               	lda xaml
   144  cfaa 20d4cf             	jsr prbyte
   145  cfad a9ba               	lda #$BA ; colon
   146  cfaf 20e7cf             	jsr echo
   147                          prdata:
   148  cfb2 a920               	lda #32
   149  cfb4 20e7cf             	jsr echo
   150  cfb7 a1a3               	lda (xaml,x)
   151  cfb9 20d4cf             	jsr prbyte
   152                          xamnext:
   153  cfbc 86aa               	stx mode
   154  cfbe a5a3               	lda xaml
   155  cfc0 c5a7               	cmp wl
   156  cfc2 a5a4               	lda xamh
   157  cfc4 e5a8               	sbc wh
   158  cfc6 b0c1               	bcs tonextitem
   159  cfc8 e6a3               	inc xaml
   160  cfca d002               	bne mod8chk
   161  cfcc e6a4               	inc xamh
   162                          mod8chk:
   163  cfce a5a3               	lda xaml
   164  cfd0 2907               	and #7
   165  cfd2 10c8               	bpl nxtprnt ; should always branch
   166                          prbyte:
   167  cfd4 48                 	pha
   168  cfd5 4a                 	lsr
   169  cfd6 4a                 	lsr
   170  cfd7 4a                 	lsr
   171  cfd8 4a                 	lsr
   172  cfd9 20ddcf             	jsr prhex
   173  cfdc 68                 	pla
   174                          prhex:
   175  cfdd 290f               	and #$0F
   176  cfdf 09b0               	ora #$B0
   177  cfe1 c9ba               	cmp #$BA
   178  cfe3 9002               	bcc echo
   179  cfe5 6906               	adc #6
   180                          echo:
   181                          ;** C64, etc. support added by David R. Van Wagner davevw.com ***************************************
   182  cfe7 297f               	and #$7f ; strip mark bit
   183  cfe9 c920               	cmp #32	; space?
   184  cfeb d010               	bne notspace
   185  cfed ad80ff             	lda $FF80 ; Commodore ROM version
   186  cff0 c916               	cmp #$16  ; VIC?
   187  cff2 d007               	bne notvic
   188  cff4 a5c7               	lda 199
   189  cff6 4912               	eor #18	; invert reverse state
   190  cff8 85c7               	sta 199
   191  cffa 60                 	rts
   192                          notvic:
   193  cffb a920                   lda #32
   194                          notspace:
   195  cffd 4cd2ff             	jmp CHROUT
   196                          ;** C64, etc. support added by David R. Van Wagner davevw.com ***************************************
